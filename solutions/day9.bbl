Program evalFile: "../utils/aoc.bbl".

struct Set buckets.
impl Set {
    static func newWithBuckets: n {
        set = Set buckets: #{}.
        n times: [ set buckets append: #{} ].
        set
    }

    func bucketFor: item {
        self buckets get: (item hash $ modulo: self buckets length)
    }

    func insert: item {
        bucket = self bucketFor: item.
        bucket contains: item $ ifFalse: [ bucket append: item ].
    }

    func length {
        self buckets map: &length $ sum
    }
}

enum Direction {
    Up.
    Down.
    Left.
    Right.
}

struct Point x y.
impl Point {
    static func zero {
        Point x: 0 y: 0
    }

    func distanceTo: other {
        #{ (self x - other x $ abs). (self y - other y $ abs) }
    }

    func right {
        Point x: (self x + 1) y: y
    }

    func clone {
        Point x: self x y: self y
    }

    func hash {
        self x * self y $ abs
    }
}

struct Rope head tail.
impl Rope {
    func move: dir {
        oldHead = self head clone.
        dir match mustBeOneOf: #{
            ?[ | Direction#Up    | self head y = self head y - 1 ]
            ?[ | Direction#Down  | self head y = self head y + 1 ]
            ?[ | Direction#Left  | self head x = self head x - 1 ]
            ?[ | Direction#Right | self head x = self head x + 1 ]
        }.
        self recalculateTail: oldHead.
    }

    func recalculateTail: oldHead {
        // Note: head should've been moved before calling this
        // also expects one movement at a time
        moveTail = false.

        // The difference movement cases between the head and tail are...
        self head distanceTo: self tail $ match mustBeOneOf: #{
            // They're in the same place - nothing needs to happen
            ?[ | #{ 0 0 } | ]

            // They're immediately adjacent - nothing needs to happen
            ?[ | #{ 1 0 } | ]
            ?[ | #{ 0 1 } | ]

            // They're immediately diagonal - nothing needs to happen
            ?[ | #{ 1 1 } | ]
            ?[ | #{ 1 1 } | ]

            // The head is further away than any of these - movement needed!
            ?[ |_| moveTail = true ]
        }.

        moveTail ifTrue: [ self tail = oldHead ]
    }
}

struct Day9.
impl Day9 {
    static func part1 {
        input = AOC readInput: 9 $ split: "\n" $ map: [ |line| line split: " " ].
        moves = #{}.
        input forEach: [ |i|
            move = i first match mustBeOneOf: #{
                ?[ | "U" | Direction#Up    ]
                ?[ | "D" | Direction#Down  ]
                ?[ | "L" | Direction#Left  ]
                ?[ | "R" | Direction#Right ]
            }.
            i last toInteger times: [
                moves append: move
            ].
        ].

        rope = Rope head: Point zero tail: Point zero.
        visitedPoints = Set newWithBuckets: 1000.
        visitedPoints insert: Point zero.
        moves forEach: [ |move|
            rope move: move.
            visitedPoints insert: rope tail.
        ].
        visitedPoints length
    }
}

Console println: Day9 part1.
