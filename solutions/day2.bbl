Program evalFile: "../utils/aoc.bbl".

/// The result of a match, from your perspective.
enum Outcome {
    Win.
    Loss.
    Draw.
}
impl Outcome {
    /// Converts a character from the input to the corresponding play. (For part 2)
    static func parse: char {
        char match mustBeOneOf: #{
            ?[ |"X"| #Loss ]
            ?[ |"Y"| #Draw ]
            ?[ |"Z"| #Win  ]
        }
    }

    /// The score achieved from this outcome. (Must be multiplied with your play score to get a
    /// total score for a round.)
    func score {
        self match mustBeOneOf: #{
            ?[ | #Win  | 6 ]
            ?[ | #Loss | 0 ]
            ?[ | #Draw | 3 ]
        }
    }

    /// Determines the `Play` that you need to use against your opponent's given play, in order to
    /// achieve this outcome.
    func playAgainstToAchieve: other {
        // Try each play until we find the one which gives this outcome
        #{ Play#Rock. Play#Paper. Play#Scissors }
            find: [ |myPlay| myPlay outcomeVersus: other $ equals: self ]
    }
}

/// The play made by a player.
enum Play {
    Rock.
    Paper.
    Scissors.
}
impl Play {
    /// Converts a character from the input to the corresponding play.
    static func parse: char {
        char match mustBeOneOf: #{
            ?[ |"A"| #Rock     ]
            ?[ |"X"| #Rock     ]
            ?[ |"B"| #Paper    ]
            ?[ |"Y"| #Paper    ]
            ?[ |"C"| #Scissors ]
            ?[ |"Z"| #Scissors ]
        }
    }

    /// Determines the `Outcome` if you play this against your opponent's given play.
    func outcomeVersus: other {
        #{ self. other } match mustBeOneOf: #{
            ?[ | #{ #Rock. #Scissors } | Outcome#Win ]
            ?[ | #{ #Scissors. #Rock } | Outcome#Loss ]

            ?[ | #{ #Paper. #Rock } | Outcome#Win ]
            ?[ | #{ #Rock. #Paper } | Outcome#Loss ]

            ?[ | #{ #Scissors. #Paper } | Outcome#Win ]
            ?[ | #{ #Paper. #Scissors } | Outcome#Loss ]

            ?[ | _ | Outcome#Draw ]
        }
    }

    /// The score achieved from this play. (Must be multiplied with your outcome score to get a
    /// total score for a round.)
    func score {
        self match mustBeOneOf: #{
            ?[ | #Rock     | 1 ]
            ?[ | #Paper    | 2 ]
            ?[ | #Scissors | 3 ]
        }
    }
}

struct Day2.
impl Day2 {
    static func part1 {
        AOC readInput: 2
            $ split: "\n"
            $ map: [ |l|
                plays = l split: " ".
                theirPlay = Play parse: plays first.
                myPlay = Play parse: plays last.

                myPlay outcomeVersus: theirPlay $ score $ + myPlay score.
            ]
            $ sum
    }

    static func part2 {
        AOC readInput: 2
            $ split: "\n"
            $ map: [ |l|
                parts = l split: " ".
                theirPlay = Play parse: parts first.
                myOutcome = Outcome parse: parts last.
                myPlay = myOutcome playAgainstToAchieve: theirPlay.

                myPlay outcomeVersus: theirPlay $ score $ + myPlay score.
            ]
            $ sum
    }
}

Console println: Day2 part1.
Console println: Day2 part2.
